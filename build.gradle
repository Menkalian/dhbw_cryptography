plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

group 'dhbw'
version '1.0'

repositories {
    mavenCentral()
}

javafx {
    version = "15.0.1"
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

mainClassName = 'application.GUI'

run {
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

// Build all components when building the project
task buildComponents(type: Task, group: "build") {
    // Always run build of components (they cache it themselves)
    outputs.upToDateWhen { false }

    doLast {
        def rootFile = new File(projectDir, "components")
        recurseFiles(rootFile)
    }
}

tasks.compileJava.dependsOn(buildComponents)

// Clean all components when cleaning the project
task cleanComponents(type: Task, group: "build") {
    // Always run build of components (they cache it themselves)
    outputs.upToDateWhen { false }

    doLast {
        def rootFile = new File(projectDir, "components")
        recurseFiles(rootFile, "clean")
    }
}

tasks.clean.dependsOn(cleanComponents)

void recurseFiles(File base, String command = "build") {
    if (base.exists() && base.isDirectory()) {
        if (base.list().contains("build.gradle")) {
            Runtime.runtime.exec("./gradlew.bat $command", null, base)
        }
        else {
            base.listFiles().each { it -> recurseFiles(it, command) }
        }
    }
}

test {
    useJUnitPlatform()
}